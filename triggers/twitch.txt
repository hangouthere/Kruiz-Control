####################################################################
# Twitch Commands
#
# Be sure to have the following OAuth Roles allocated for your Access Token:
# (see `twitchApi.txt` for more info)
#
# - moderator:manage:shoutouts

OnInit
    # Set Browser Source name for Twitch Embeds (ie, clips, stream preview, etc)
    Variable Set _OBS_Embed_Source_ "Twitch Embed Browser Source"

# Say a Shoutout Aloud
OnAction ShoutoutVoice
    Param Copy in1 messageToSpeak
    Param Create Voice "Google UK English Female"
    Param Replace messageToSpeak '@' ''
    TTS {Voice} - - - nowait {messageToSpeak}

# Displays a random clip of a Broadcaster in OBS
# Note: Utilizes a Filter that does a Fade In/Out with Advanced Scene Switcher's Move Value
OnAction PlayRandomTwitchClip
    # First we have to get the BroadcasterId for whom we want to show a clip
    Param Copy in1 Username
    GetRandomTwitchClip {Username}
    # Display in OBS for the Duration returned by the API
    Function 'return {_TwitchAPI_Found_ClipData_}'
    Variable Load _OBS_Embed_Source_
    OBS Source {_OBS_Embed_Source_} URL "https://clips.twitch.tv/embed?autoplay=true&clip={id}&controls=false&parent=localhost"
    Chat Send "Clip : {title}, clipped by {creator_name} ({duration}s) [ Link: {url} ]"
    Delay 1.5
    OBS Source {_OBS_Embed_Source_} Filter "Fade In" on
    Delay {duration}
    OBS Source {_OBS_Embed_Source_} Filter "Fade Out" on
    Delay 5
    OBS Source {_OBS_Embed_Source_} URL "about:blank"

# Perform a Shoutout in Chat and TwitchAPI
# Supports @username and username alike!
OnCommand bm 0 !so !shoutout
    Param Copy after ShoutoutUser
    Param Replace ShoutoutUser "@" ""
    GetChannelInfo {ShoutoutUser}
    # Break out if errored, already displayed the error in chat
    If {_TwitchAPI_Response_} != "error"
    Function 'return {_TwitchAPI_Response_}.data[0]'
    ChooseRandomLineFromFile "shoutouts"
    Chat Send "{_randomChosen_} They were last seen doing some {game_name} - {title}"
    ShoutoutVoice {_randomChosen_}
    TwitchShoutout {ShoutoutUser}
    PlayRandomTwitchClip {ShoutoutUser}