####################################################################
# Twitch Commands
#

OnInit
    # Set Browser Source name for Twitch Embeds (ie, clips, stream preview, etc)
    Variable Set _OBS_Embed_Source_ "Twitch Embed Browser Source"
    # Reset state of Shoutout Browser Source
    OBS Source {_OBS_Embed_Source_} Filter "Fade Out" on
    OBS Source {_OBS_Embed_Source_} URL "about:blank"

# Say a Shoutout Aloud
OnAction ShoutoutVoice
    Param Copy in1 messageToSpeak
    Param Create Voice "Google UK English Female"
    Param Replace messageToSpeak '@' ''
    TTS {Voice} - - - nowait {messageToSpeak}

# Using KC, gets top 100 clips, and picks one by random, returning the API response data as variables
# See: https://dev.twitch.tv/docs/api/reference/#get-clips
OnAction GetRandomTwitchClip
    Param Copy in1 Username
    Param Replace Username "@" ""
    Twitch ClipsByUser {Username} 100
    Function 'const clipList = [data].data; return clipList[Math.floor(Math.random() * clipList.length)];'

# Displays a random clip of a Broadcaster in OBS
# Note: Utilizes a Filter that does a Fade In/Out with Advanced Scene Switcher's Move Value
OnAction PlayRandomTwitchClip
    # First we have to get the BroadcasterId for whom we want to show a clip
    Param Copy in1 Username
    GetRandomTwitchClip {Username}
    Variable Load _OBS_Embed_Source_
    # If no {id} is found, that means no clip was returned, and we should prematurely exit the trigger (default IF action)
    Param Exists id
    if 1 true = {exists}
        ShowTwitchClip

# Shows last loaded Twitch Clip Information
OnAction ShowTwitchClip
    OBS Source {_OBS_Embed_Source_} URL "https://clips.twitch.tv/embed?autoplay=true&clip={id}&controls=false&parent=localhost"
    Chat Send "Clip : {title}, clipped by {creator_name} ({duration}s) [ Link: {url} ]"
    Delay 1.5
    OBS Source {_OBS_Embed_Source_} Filter "Fade In" on
    Delay {duration}
    OBS Source {_OBS_Embed_Source_} Filter "Fade Out" on
    Delay 5
    OBS Source {_OBS_Embed_Source_} URL "about:blank"

# Copies a Twitch Reward
OnCommand b 0 !twitchCopy
    Twitch Copy {after}

# PretzelRocks messages are auto-deleted, because we hate seeing them!
OnEveryChatMessage
    If {user} = "PretzelRocks"
    Twitch DeleteMessage {message_id}
    Exit

OnTWChannelPoint "Light Me Up"
    LightSolid "{message}" "60000"

# Perform a Shoutout in Chat and TwitchAPI
# Supports @username and username alike!
OnAction TwitchShoutOut
    Param Copy in1 ShoutoutUser
    Param Replace ShoutoutUser "@" ""
    # If the Twitch User is invalid, KC will bomb exit here and not continue
    Twitch User {ShoutoutUser}
    Twitch ChannelInfo {ShoutoutUser}
    ChooseRandomLineFromFile "shoutouts"
    Twitch Shoutout {ShoutoutUser}
    Twitch Announcement "{_randomChosen_} They were last seen doing some {game} - {title}" orange
    # Clip data comes from a previous API call (PlayRandomTwitchClip -> GetRandomTwitchClip -> Twitch ClipsByUser -> Function)
    PlayRandomTwitchClip {ShoutoutUser}
    ShoutoutVoice {_randomChosen_}

# Do a ShoutOut and set up a Raid
OnAction RaidStream
    Param Copy in1 stream
    TwitchShoutOut {stream}
    Twitch Raid {stream}

OnCommand bm 0 !so !shoutout
    TwitchShoutOut {after}

OnCommand bm 0 !raid
    RaidStream {after}

# Pick a Stream currently online that is streaming the same game as Me
OnCommand bm 0 !randomRaid
    Twitch ChannelInfo
    Param Copy game MyGame
    Twitch Streams
    if 2 {stream_count} = 0
        Chat Send "No Friends are streaming at this moment."
        Exit
    Param Create i 1
    List Empty CurrentStreamers
    Loop 3 {stream_count}
        if 1 {MyGame} = {game{i}}
            List Add CurrentStreamers {stream{i}}
        Param Add i 1
    List Count CurrentStreamers
    if 2 {count} = 0
        Chat Send "No Friends are streaming at this moment."
        Exit
    List Get CurrentStreamers
    RaidStream {value}