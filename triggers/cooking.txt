# Action to set the Menu Browser Source
OnAction SetMenu
    Param Copy in1 MenuUUID
    Param Exists MenuUUID
    If 2 {exists} = false
        Chat Send "You must supply a Menu UUID"
        Exit
    #
    Param Create URL "http://127.0.0.1:5500/index.html"
    OBS Source "Menu Browser Source"  URL "{URL}?menu={MenuUUID}"
    Chat Send "Set Menu: {MenuUUID}"

# Action to set the Recipe Browser Source
OnAction SetRecipe
    Param Copy in1 RecipeUUID
    Param Exists RecipeUUID
    If 2 {exists} = false
        Chat Send "You must supply a Recipe UUID"
        Exit
    #
    Param Create URL "http://127.0.0.1:5501/index.html"
    OBS Source "Recipe Browser Source"  URL "{URL}?recipe={RecipeUUID}"
    Chat Send "Set Recipe: {RecipeUUID}"

# Command for setting the current Menu and *optionally* the first recipe
OnCommand sm 0 !setMenu
    SetMenu {arg1}
    # Set Recipe if passed in
    Param Copy arg2 RecipeUUID
    Param Exists RecipeUUID
    If {exists} = true
    SetRecipe {RecipeUUID}

# Command for setting the current Recipe
OnCommand sm 0 !setRecipe
    SetRecipe {arg1}

# Chatter command for showing Menu (if allowed)
OnCommand e 60 !menu !showMenu
    Variable Global Load IsCookingShow
    if 2 {IsCookingShow} = "No variable found"
        Chat Send "Sorry, we're not cooking during this stream!"
        Exit
    OBS Send "ChatShowMenu"

# Chatter command for showing Recipe (if allowed)
OnCommand e 60 !recipe !showRecipe
    Variable Global Load IsCookingShow
    if 2 {IsCookingShow} = "No variable found"
        Chat Send "Sorry, we're not cooking during this stream!"
        Exit
    OBS Send "ChatShowRecipe"

# Toggle Cooking Show abilities
OnCommand sm 0 !cooking
    Param Keyword after "on" "yes" "enable"
    if 2 {matched} = true
        Variable Global Set IsCookingShow 1
        Chat Send "Enabled Cooking Show!"
    #
    Param Keyword after "off" "no" "disable"
    if 2 {matched} = true
        Variable Global Remove IsCookingShow
        Chat Send "Disabled Cooking Show!"

# Streamer/Mod Command for showing Menu (auto enables Cooking Show)
OnCommand sm 0 !mmenu !mshowMenu
    Variable Global Set IsCookingShow 1
    OBS Send "ChatShowMenu"

# Streamer/Mod Command for showing Recipe (auto enables Cooking Show)
OnCommand sm 0 !mrecipe !mshowRecipe
    Variable Global Set IsCookingShow 1
    OBS Send "ChatShowRecipe"
